buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://repo.spring.io/milestone' }
    }
    dependencies {
        def freefairGradleVersion = "6.4.3"
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.6.6") {
            exclude group: "com.squareup.okhttp3"
        }
        classpath "io.freefair.gradle:maven-plugin:${freefairGradleVersion}"
        classpath "io.freefair.gradle:lombok-plugin:${freefairGradleVersion}"
        classpath "io.freefair.gradle:okhttp-plugin:${freefairGradleVersion}"
        classpath "io.freefair.gradle:git-plugin:${freefairGradleVersion}"
    }
}

plugins {
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

apply plugin: "io.freefair.git-version"
apply plugin: "io.freefair.aggregate-javadoc-jar"
apply plugin: "io.freefair.javadoc-links"

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
    }

    group = "io.freefair.okhttp-spring-boot"

    plugins.withId("java") {
        java {
            withSourcesJar()
            withJavadocJar()
            toolchain {
                languageVersion = JavaLanguageVersion.of(8)
            }
        }

        tasks.named('jar', Jar) {
            into("META-INF") {
                from rootProject.file("LICENSE")
            }
        }
    }

    tasks.withType(Javadoc).configureEach {
        options.addBooleanOption "Xdoclint:-missing", true
    }

    plugins.withId("maven-publish") {

        project.apply plugin: "signing"
        project.apply plugin: "io.freefair.maven-central.validate-poms"

        signing {
            required { !version.endsWith('SNAPSHOT') && gradle.taskGraph.hasTask("publish") }

            def signingKey = findProperty("signingKey")
            def signingPassword = findProperty("signingPassword")
            useInMemoryPgpKeys(signingKey, signingPassword)
        }

        publishing {
            publications.withType(MavenPublication) {
                pom {
                    name = provider { project.description }
                    description = provider { project.description }
                    url = "https://github.com/freefair/okhttp-spring-boot"
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'larsgrefer'
                            name = 'Lars Grefer'
                            email = 'github@larsgrefer.de'
                            timezone = 'Europe/Berlin'
                        }
                    }
                    ciManagement {
                        system = 'GitHub Actions'
                        url = 'https://github.com/freefair/okhttp-spring-boot/actions'
                    }
                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/freefair/okhttp-spring-boot/issues'
                    }
                    scm {
                        connection = 'scm:git:https://github.com/freefair/okhttp-spring-boot.git'
                        developerConnection = 'scm:git:git@github.com:freefair/okhttp-spring-boot.git'
                        url = 'https://github.com/freefair/okhttp-spring-boot'
                    }
                }
            }
        }
    }
}

apply plugin: "maven-publish"

nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = '7e6204597a774f'
        }
    }
}

apply plugin: "io.freefair.okhttp"

tasks.register('uploadDocumentation', io.freefair.gradle.plugins.okhttp.tasks.UploadFile) {
    dependsOn "aggregateJavadocJar"
    username = "user"
    password = findProperty('freefairDocsPass')
    file = aggregateJavadocJar.archiveFile
    url = "https://docs.freefair.io/api/$project.version?path=okhttp-spring-boot"
}

tasks.named('wrapper') {
    distributionType = Wrapper.DistributionType.ALL
}
