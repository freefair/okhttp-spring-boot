import io.freefair.gradle.plugins.okhttp.tasks.UploadFile

plugins {
    id "org.springframework.boot" version "3.2.8" apply false
    id "io.github.gradle-nexus.publish-plugin" version "2.0.0"
    id "io.freefair.git-version"
    id "io.freefair.okhttp"
    id "io.freefair.maven-central.validate-poms" apply false
    id "io.freefair.aggregate-javadoc"
}

allprojects {

    group = "io.freefair.okhttp-spring-boot"

    plugins.withId("java") {
        java {
            withSourcesJar()
            withJavadocJar()
            toolchain {
                languageVersion = JavaLanguageVersion.of(17)
            }
        }

        tasks.named('jar', Jar) {
            into("META-INF") {
                from rootProject.file("LICENSE")
            }
        }
    }

    tasks.withType(Javadoc).configureEach {
        options.addBooleanOption "Xdoclint:-missing", true
        failOnError = false
    }

    plugins.withId("maven-publish") {

        project.apply plugin: "signing"
        project.apply plugin: "io.freefair.maven-central.validate-poms"

        signing {
            required { !version.endsWith('SNAPSHOT') && gradle.taskGraph.hasTask("publish") }

            def signingKey = findProperty("signingKey")
            def signingPassword = findProperty("signingPassword")
            useInMemoryPgpKeys(signingKey, signingPassword)
        }

        publishing {
            publications.withType(MavenPublication) {
                pom {
                    name = provider { project.description }
                    description = provider { project.description }
                    url = "https://github.com/freefair/okhttp-spring-boot"
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'larsgrefer'
                            name = 'Lars Grefer'
                            email = 'github@larsgrefer.de'
                            timezone = 'Europe/Berlin'
                        }
                    }
                    ciManagement {
                        system = 'GitHub Actions'
                        url = 'https://github.com/freefair/okhttp-spring-boot/actions'
                    }
                    issueManagement {
                        system = 'GitHub Issues'
                        url = 'https://github.com/freefair/okhttp-spring-boot/issues'
                    }
                    scm {
                        connection = 'scm:git:https://github.com/freefair/okhttp-spring-boot.git'
                        developerConnection = 'scm:git:git@github.com:freefair/okhttp-spring-boot.git'
                        url = 'https://github.com/freefair/okhttp-spring-boot'
                    }
                }
            }
        }
    }
}

apply plugin: "maven-publish"

nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = '7e6204597a774f'
        }
    }
}

dependencies {
    javadoc project(":okhttp-spring-client")
    javadoc project(":okhttp-spring-boot-autoconfigure")
    javadoc project(":starters:okhttp-spring-boot-starter")

    javadocClasspath "org.springframework.boot:spring-boot-starter-actuator"
    javadocClasspath 'com.squareup.okhttp3:logging-interceptor'
}

tasks.named('javadoc', Javadoc) {
    options.locale "en_US"
    options.linkSource true
    options.links "https://square.github.io/okhttp/3.x/"
    failOnError = false
}

tasks.register('uploadDocumentation', UploadFile) {
    dependsOn ":javadocJar"
    username = "user"
    password = findProperty('freefairDocsPass')
    file = tasks.named("javadocJar", Jar).flatMap {it.archiveFile}
    url = "https://docs.freefair.io/api/$project.version?path=okhttp-spring-boot"
}

tasks.named('wrapper') {
    distributionType = Wrapper.DistributionType.ALL
}
