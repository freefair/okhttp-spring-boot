plugins {
    id "org.springframework.boot" version "2.7.18" apply false
    id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
    id "io.freefair.git-version"
    id "io.freefair.okhttp"
    id "io.freefair.maven-central.validate-poms" apply false
}

allprojects {
    group = "io.freefair.okhttp-spring-boot"

    plugins.withId("java") {
        java {
            withSourcesJar()
            withJavadocJar()
            toolchain {
                languageVersion = JavaLanguageVersion.of(8)
            }
        }

        tasks.named('jar', Jar) {
            into("META-INF") {
                from rootProject.file("LICENSE")
            }
        }
    }

    tasks.withType(Javadoc).configureEach {
        options.addBooleanOption "Xdoclint:-missing", true
        failOnError = false
    }

    plugins.withId("maven-publish") {

        project.apply plugin: "signing"
        project.apply plugin: "io.freefair.maven-central.validate-poms"

        signing {
            required { !version.endsWith('SNAPSHOT') && gradle.taskGraph.hasTask("publish") }

            def signingKey = findProperty("signingKey")
            def signingPassword = findProperty("signingPassword")
            useInMemoryPgpKeys(signingKey, signingPassword)
        }

        publishing {
            publications.withType(MavenPublication) {
                pom {
                    name = provider { project.description }
                    description = provider { project.description }
                    url = "https://github.com/freefair/okhttp-spring-boot"
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'larsgrefer'
                            name = 'Lars Grefer'
                            email = 'github@larsgrefer.de'
                            timezone = 'Europe/Berlin'
                        }
                    }
                    ciManagement {
                        system = 'GitHub Actions'
                        url = 'https://github.com/freefair/okhttp-spring-boot/actions'
                    }
                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/freefair/okhttp-spring-boot/issues'
                    }
                    scm {
                        connection = 'scm:git:https://github.com/freefair/okhttp-spring-boot.git'
                        developerConnection = 'scm:git:git@github.com:freefair/okhttp-spring-boot.git'
                        url = 'https://github.com/freefair/okhttp-spring-boot'
                    }
                }
            }
        }
    }
}

apply plugin: "maven-publish"

nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = '7e6204597a774f'
        }
    }
}

tasks.register('uploadDocumentation', io.freefair.gradle.plugins.okhttp.tasks.UploadFile) {
    dependsOn ":autoconfigure:javadocJar"
    username = "user"
    password = findProperty('freefairDocsPass')
    file = tasks.findByPath(":autoconfigure:javadocJar").archiveFile
    url = "https://docs.freefair.io/api/$project.version?path=okhttp-spring-boot"
}

tasks.named('wrapper') {
    distributionType = Wrapper.DistributionType.ALL
}
