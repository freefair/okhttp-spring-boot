pluginManagement {
    repositories {
        gradlePluginPortal()
        maven { url 'https://repo.spring.io/milestone' }
    }
}

plugins {
    id "io.freefair.settings.plugin-versions" version "8.4"
    id "com.gradle.enterprise" version "3.15.1"
}

boolean isCiServer = System.getenv().containsKey("CI")

gradleEnterprise {
    if (isCiServer) {
        buildScan {
            publishAlways()
            termsOfServiceUrl = "https://gradle.com/terms-of-service"
            termsOfServiceAgree = "yes"
        }
    }
}

buildCache {
    remote(HttpBuildCache) {
        url = 'https://build-cache.grefer-hosting.de/cache/'
        push = isCiServer
        credentials {
            username = "freefair"
            password = providers.gradleProperty('freefairBuildCachePassword').getOrElse(null)
        }
    }
}

dependencyResolutionManagement {
    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
    }
}

include 'okhttp-spring-client'

include 'dependencies'
include 'okhttp-spring-boot-autoconfigure'
include 'starters:okhttp'
include 'starters:okhttp4'
include 'starters:okhttp5'

rootProject.name = 'okhttp-spring-boot'
