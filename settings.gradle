plugins {
    id "io.freefair.settings.plugin-versions" version "8.3"
    id "com.gradle.enterprise" version "3.15"
}

boolean isCiServer = System.getenv().containsKey("CI")

gradleEnterprise {
    if (isCiServer) {
        buildScan {
            publishAlways()
            termsOfServiceUrl = "https://gradle.com/terms-of-service"
            termsOfServiceAgree = "yes"
        }
    }
}

buildCache {
    remote(HttpBuildCache) {
        url = 'https://build-cache.grefer-hosting.de/cache/'
        push = isCiServer
        credentials {
            username = "freefair"
            password = providers.gradleProperty('freefairBuildCachePassword').getOrElse(null)
        }
    }
}

dependencyResolutionManagement {
    repositories {
        mavenCentral()
    }
}

include 'dependencies'
include 'autoconfigure'
include 'starters:okhttp'
include 'starters:okhttp4'
include 'starters:okhttp5'

rootProject.name = 'okhttp-spring-boot'
