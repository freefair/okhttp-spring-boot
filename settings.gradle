pluginManagement {
    repositories {
        gradlePluginPortal()
        maven { url 'https://repo.spring.io/milestone' }
    }
}

plugins {
    id "io.freefair.settings.plugin-versions" version "8.6"
    id "com.gradle.enterprise" version "3.17.4"
}

boolean isCiServer = System.getenv().containsKey("CI")

gradleEnterprise {
    if (isCiServer) {
        buildScan {
            publishAlways()
            termsOfServiceUrl = "https://gradle.com/terms-of-service"
            termsOfServiceAgree = "yes"
        }
    }
}

buildCache {
    remote(HttpBuildCache) {
        url = 'https://build-cache.grefer-hosting.de/cache/'
        push = isCiServer
        credentials {
            username = "freefair"
            password = providers.gradleProperty('freefairBuildCachePassword').getOrElse(null)
        }
    }
}

dependencyResolutionManagement {
    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
    }
}

include 'okhttp-spring-client'

include 'okhttp-spring-boot-dependencies'
include 'okhttp-spring-boot-autoconfigure'
include 'starters:okhttp-spring-boot-starter'
include 'starters:okhttp4-spring-boot-starter'
include 'starters:okhttp5-spring-boot-starter'

rootProject.name = 'okhttp-spring-boot'
