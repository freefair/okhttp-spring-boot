plugins {
    id "checkstyle"
    id "jacoco"
    id "io.freefair.lombok"
    id "java-library"
}
apply plugin: "io.freefair.maven-optional"
apply plugin: "io.freefair.javadoc-links"
apply plugin: "io.freefair.maven-publish-java"

description = "OkHttp Spring Boot AutoConfigure"

publishing.publications.mavenJava.artifactId = "okhttp-spring-boot-autoconfigure"

javadoc.options.linkSource true

if (JavaVersion.current().isJava11Compatible()) {
    javadoc.failOnError = false
}

compileJava.dependsOn(processResources)

sourceCompatibility = 1.8
targetCompatibility = 1.8

jacocoTestReport.reports.xml.enabled = true

dependencies {
    api platform(project(":dependencies"))
    annotationProcessor platform(project(":dependencies"))

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.springframework.boot:spring-boot-autoconfigure-processor'

    api 'org.springframework.boot:spring-boot-autoconfigure'
    implementation 'org.slf4j:slf4j-api'

    optional 'org.springframework:spring-web'

    optional 'com.squareup.okhttp3:okhttp:4.+'
    optional 'com.squareup.okhttp3:logging-interceptor:3.+'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}
